apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "dev.weiqi.androidtravisci"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../keystore/key.jks")

            def isRunningOnTravis = System.getenv("CI") == "true"
            if (isRunningOnTravis) {
                keyAlias System.getenv('KEYSTORE_ALIAS')
                keyPassword System.getenv('KEYSTORE_KEY_PASSWORD')
                storePassword System.getenv('KEYSTORE_PASSWORD')
            } else {
                def keyProperties = file("../local.properties")
                if (keyProperties.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(keyProperties))
                    keyAlias props['KEYSTORE_ALIAS']
                    keyPassword props['KEYSTORE_KEY_PASSWORD']
                    storePassword props['KEYSTORE_PASSWORD']
                }
            }

        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}